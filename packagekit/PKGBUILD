# $Id: PKGBUILD 102883 2013-12-23 10:36:15Z bgyorgy $
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>
pkgbase='packagekit'
pkgname=('packagekit' 'packagekit-glib2' 'python2-packagekit')
pkgver=0.8.16
pkgrel=11
pkgdesc="A system designed to make installation and updates of packages easier."
arch=('i686' 'x86_64')
url="http://www.packagekit.org"
license=('GPL')
makedepends=('dbus-glib' 'gobject-introspection' 'gtk-doc' 'intltool'
             'networkmanager' 'pacman'  'pm-utils' 'polkit' 'python2' 'qt4'
             'shared-mime-info' 'sqlite' 'udev')
source=("http://www.packagekit.org/releases/PackageKit-$pkgver.tar.xz")
sha256sums=('16a17382b0acd02a5b059e4cd0b46d18ef6d86e304a47da381d446d7cac1b9d9')

prepare() {
  cd "$srcdir/PackageKit-$pkgver"

  sed -i 's@SUBDIRS = test@SUBDIRS =@' 'backends/Makefile.in'
  sed -i 's@python @python2 @' 'lib/python/packagekit/Makefile.in'
  sed -i 's@bin/python@bin/python2@' 'lib/python/packagekit/'*.py
}

build() {
  cd "$srcdir/PackageKit-$pkgver"

  export PYTHON=/usr/bin/python2
  ./autogen.sh --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/lib/PackageKit \
              --enable-strict \
              --disable-static \
              --disable-gtk-doc \
              --disable-tests \
              --disable-local \
              --disable-browser-plugin \
              --disable-gstreamer-plugin \
              --disable-gtk-module \
              --disable-command-not-found \
              --disable-cron \
              --disable-debuginfo-install \
              --enable-pm-utils \
              --disable-dummy \
              --disable-alpm
  make -s CFLAGS='-D_FILE_OFFSET_BITS=64 -O2 -Wno-unused-local-typedefs -Wno-deprecated-declarations -Wno-suggest-attribute=format'
}

package_packagekit() {
  backup=('var/lib/PackageKit/transactions.db'
          'etc/PackageKit/alpm.d/pacman.conf'
          'etc/PackageKit/alpm.d/repos.list')
  depends=('dbus-glib' 'pacman>=4.1.0' 'pacman<4.2.0' 'polkit'
           'shared-mime-info' 'sqlite' 'udev')
  optdepends=('networkmanager')
  install='packagekit.install'

  cd "$srcdir/PackageKit-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/lib/python"*
  rm -rf "$pkgdir/usr/share/PackageKit/website"
  
  rm -rf "$pkgdir/usr/include/PackageKit/packagekit-glib2"
  rm -rf "$pkgdir/usr/lib/girepository-1.0/PackageKitGlib-1.0.typelib"
  rm -rf "$pkgdir/usr/lib/libpackagekit-glib2.so"*
  rm -rf "$pkgdir/usr/lib/pkgconfig/packagekit-glib2.pc"
  rm -rf "$pkgdir/usr/share/gir-1.0/PackageKitGlib-1.0.gir"

  touch "$pkgdir/var/log/PackageKit"
}

package_packagekit-glib2() {
  depends=('packagekit' 'glib2')
  pkgdesc=('glib bindings for PackageKit')

  cd "$srcdir/PackageKit-$pkgver/lib/packagekit-glib2"

  make DESTDIR="$pkgdir" install
}

package_python2-packagekit() {
  depends=('packagekit' 'python2')
  pkgdesc=('Python bindings for PackageKit')
  conflicts=('packagekit-python')
  replaces=('packagekit-python')

  cd "$srcdir/PackageKit-$pkgver/lib/python"

  make DESTDIR="$pkgdir" install
}

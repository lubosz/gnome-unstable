# $Id: PKGBUILD 199399 2013-11-11 22:34:22Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Lubosz Sarnecki <lubosz@gmail.com>

pkgname=bijiben
pkgver=3.11.90
pkgrel=1
pkgdesc="Write out notes, every detail matters"
arch=(i686 x86_64)
url="https://live.gnome.org/Bijiben"
license=(GPL)
depends=(tracker clutter-gtk zeitgeist webkitgtk hicolor-icon-theme desktop-file-utils gnome-online-accounts)
makedepends=(intltool itstool)
options=(!emptydirs)
groups=(gnome-extra)
install=bijiben.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('60c5da94eac8c12a156e2267358f329d261d2efb57cab00b880761db42b1cdfd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

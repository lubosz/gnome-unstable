# $Id: PKGBUILD 165829 2012-08-31 06:08:20Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=webkitgtk3
pkgver=1.10.0
pkgrel=1
pkgdesc="GTK+ Web content engine library for GTK+ 3.0"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk2' 'gtk3' 'gst-plugins-base-libs')
makedepends=('gperf' 'gobject-introspection' 'python2' 'mesa' 'ruby' 'gtk-doc')
conflicts=('libwebkit3')
provides=("libwebkit3=${pkgver}")
replaces=('libwebkit3')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)
sha1sums=('5e022110cef5ff87a4c55ff0358dedd596502a84')

build() {
  cd "$srcdir/webkitgtk-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --enable-introspection \
    --disable-silent-rules \
    --libexecdir=/usr/lib/webkitgtk3 \
    --with-gstreamer=1.0
  make all stamp-po
}

package() {
  cd "$srcdir/webkitgtk-$pkgver"
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

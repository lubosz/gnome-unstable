# $Id: PKGBUILD 204483 2014-01-20 20:35:39Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=(cogl cogl-gst)
pkgver=1.17.4
pkgrel=1
pkgdesc="An object oriented GL/GLES Abstraction/Utility Layer"
arch=('i686' 'x86_64')
url="http://www.clutter-project.org/"
license=('GPL2')
depends=('mesa' 'libdrm' 'libxext' 'libxdamage' 'libxcomposite' 'gdk-pixbuf2' 'pango' 'libxrandr' 'appdata-tools')
makedepends=('gobject-introspection' 'gstreamer')
options=(!emptydirs)
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('573c4ee7335fc4d4d1578e9103191e260fcaab8a80dfd20464c70444a74cbaaf')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --enable-gles{1,2} \
    --enable-{kms,wayland}-egl-platform \
    --enable-wayland-egl-server \
    --enable-cogl-gst

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package_cogl() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  rm -rf "$pkgdir/usr/include/cogl/cogl-gst/cogl-gst-video-sink.h"
  rm -rf "$pkgdir/usr/include/cogl/cogl-gst/cogl-gst.h"
  rm -rf "$pkgdir/usr/lib/gstreamer-1.0/libgstcogl.so"
  rm -rf "$pkgdir/usr/lib/libcogl-gst.so"*
  rm -rf "$pkgdir/usr/lib/pkgconfig/cogl-gst-1.0.pc"
  rm -rf "$pkgdir/usr/lib/pkgconfig/cogl-gst-2.0-experimental.pc"
}

package_cogl-gst() {
  depends=('gstreamer')
  
  cd "cogl-$pkgver/cogl-gst"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

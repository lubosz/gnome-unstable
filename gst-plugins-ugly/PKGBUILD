# $Id: PKGBUILD 163150 2012-07-08 09:31:14Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-plugins-ugly
pkgname=('gst-plugins-ugly')
pkgver=1.0.0
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
pkgdesc="GStreamer Multimedia Framework Ugly plugins"
depends=('gst-plugins-base>=1.0.0')
makedepends=('pkgconfig' 'gst-plugins-base>=1.0.0' 'libdvdread' 'lame' 'libmpeg2' 'a52dec' 'libmad' 'libsidplay' 'libcdio' 'x264' 'opencore-amr')
url="http://gstreamer.freedesktop.org/"
options=(!libtool)
source=(${url}/src/gst-plugins-ugly/gst-plugins-ugly-${pkgver}.tar.xz)
sha256sums=('d0d21777d16afae3064e0d660cbe46eaabf889fa7f60b065ead8b3d1108a905d')

build() {
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Ugly Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
}

check() {
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

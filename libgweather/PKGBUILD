# $Id: PKGBUILD 166809 2012-09-19 14:43:44Z jgc $
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=3.5.92
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libsoup-gnome' 'gnome-icon-theme' 'gtk3')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'gnome-common')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        gettext-not-xml.patch
        no_external_gettext.patch)
sha256sums=('1f1dc6abf9dcfa38d663a4aadfc91a43bc7a5d1dd12ac68f6412c35bc40622cf'
            '40325e0b5464ebb75eaad3c9292c0bab7b15cfefb93011d9ccc0ab82635a9533'
            'f07ac07a70ad4999c3acd5d5b82aabd8831e6a45beb9aaed239f212057ded672')

build() {
    cd "$pkgname-$pkgver"

    patch -Np1 -i "$srcdir/gettext-not-xml.patch"
    patch -Np1 -i "$srcdir/no_external_gettext.patch"
    gtkdocize
    autoreconf -fi

    cd po
     intltool-update --pot
    cd ../po-locations
    intltool-update --gettext-package libgweather-locations --pot
    cd ..
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
        --enable-locations-compression
    make
}

package() {
    cd "$pkgname-$pkgver"
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
}

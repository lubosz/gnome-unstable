# $Id: PKGBUILD 167083 2012-09-25 10:11:40Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=3.6.0
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://projects.gnome.org/evince/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libgxps' 'libspectre' 'gsfonts' 'poppler-glib' 'djvulibre' 'gnome-icon-theme' 't1lib' 'libgnome-keyring' 'desktop-file-utils' 'dconf' 'gsettings-desktop-schemas')
makedepends=('itstool' 'nautilus' 'texlive-bin' 'intltool' 'gobject-introspection' 'docbook-xsl')
optdepends=('texlive-bin: DVI support')
groups=('gnome-extra')
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('8dfa7b9c2f0b910d06b1612ff445d3f3050c44d25f2e78718966374d30ab83f2')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr   --sysconfdir=/etc \
      --localstatedir=/var    --libexecdir=/usr/lib/evince \
      --disable-static        --enable-nautilus \
      --enable-pdf            --enable-tiff \
      --enable-djvu           --enable-dvi \
      --enable-t1lib          --enable-comics \
      --disable-scrollkeeper  --disable-schemas-compile \
      --enable-introspection
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

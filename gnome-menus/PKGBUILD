# $Id: PKGBUILD 167081 2012-09-25 09:50:30Z jgc $
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.6.0
pkgrel=1
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2' 'python2-gobject')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        menus.patch)
sha256sums=('523ac10729e28f9acde4785e61dcb895828fd89e39b38b68f6c14ec815805a0b'
            'df2779777906b038fa911dc745534564ede9524a64ab368e2f6f8e38d54e7acc')

build() {
  cd "$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/menus.patch"

  PYTHON=/usr/bin/python2 ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  sed -i "1s|#!/usr/bin/env python$|&2|" \
    "$pkgdir/usr/lib/python2.7/site-packages/GMenuSimpleEditor/config.py"
}

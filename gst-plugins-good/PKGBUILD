# $Id: PKGBUILD 153259 2012-03-12 16:05:31Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-plugins-good
pkgname=('gst-plugins-good')
pkgver=1.0.0
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
pkgdesc="GStreamer Multimedia Framework Good Plugins"
depends=('gst-plugins-base>=1.0.0' 'bzip2' 'libavc1394' 'libiec61883' 'aalib' 'libshout' 'libdv' 'flac' 'gconf' 'wavpack' 'taglib' 'libsoup-gnome' 'v4l-utils' 'libcaca' 'libpng' 'libjpeg' 'jack' 'libpulse' 'udev')
makedepends=('intltool' 'pkgconfig' 'gst-plugins-base>=1.0.0' 'libavc1394' 'libiec61883' 'aalib' 'libshout' 'libdv' 'flac' 'gconf' 'wavpack' 'taglib' 'libsoup-gnome' 'v4l-utils' 'libcaca' 'bzip2' 'gdk-pixbuf2' 'libpulse' 'jack' 'udev')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.xz)
sha256sums=('f4684edb098d0d60b4a3d5a1fce846a4ba351f80adac4ba6e8199bd059f87886')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Good Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"

  make
}

check() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/$pkgbase-${pkgver}"
  make DESTDIR="${pkgdir}" install
}



# $Id: PKGBUILD 153256 2012-03-12 15:48:30Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-plugins-base
pkgname=('gst-plugins-base-libs' 'gst-plugins-base')
pkgver=1.0.0
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango' 'gobject-introspection')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgbase/$pkgbase-${pkgver}.tar.xz)
sha256sums=('4b98a74712e31d5426dd1cf2de883b6af59fd37997b0ffaed53914eb6df45053')

build() {
  cd "${srcdir}/$pkgbase-${pkgver}"
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Base Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"

  make
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd "${srcdir}/gst-plugins-base-${pkgver}"
  # Testsuite fails on one test. Some refcount leak
  #make check
}

package_gst-plugins-base-libs() {
  pkgdesc="GStreamer Multimedia Framework Base plugin libraries"
  depends=('gstreamer' 'orc' 'libxv')

  cd "${srcdir}/$pkgbase-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_gst-plugins-base() {
  pkgdesc="GStreamer Multimedia Framework Base Plugins (gst-plugins-base)"
  depends=("gst-plugins-base-libs=$pkgver" 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango')

  cd "${srcdir}/$pkgbase-${pkgver}"
  make -C gst-libs DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
  make -C gst-libs DESTDIR="${pkgdir}" uninstall
}

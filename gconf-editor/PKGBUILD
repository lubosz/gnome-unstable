# $Id: PKGBUILD 150859 2012-02-23 13:04:28Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gconf-editor
pkgver=3.0.1
pkgrel=2
pkgdesc="Graphical gconf registry editor"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils')
url="http://www.gnome.org"
groups=('gnome-extra')
options=(!emptydirs)
install=gconf-editor.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('9afc4fc0d0afe019998736f06c04cbfa0393c813e2aa755133d95e83835f8869')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
     --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain gconf-editor "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}

# $Id: PKGBUILD 198462 2013-10-30 15:01:19Z allan $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=totem
pkgname=('totem' 'totem-plugin')
pkgver=3.11.90
pkgrel=1
pkgdesc="GNOME3 movie player based on GStreamer"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(GPL2 custom)
depends=('clutter>=1.17.3' gst-plugins-base gst-plugins-good totem-plparser desktop-file-utils iso-codes libpeas
         hicolor-icon-theme gnome-icon-theme gsettings-desktop-schemas dconf 'clutter-gst=2.0.10'
         'clutter-gtk=1.5.2' python-gobject python-xdg gst-plugins-bad)
makedepends=(intltool libnautilus-extension lirc-utils vala grilo dbus-glib zeitgeist
             itstool docbook-xsl python-pylint)
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz
        browser-plugins.ini bugreport.patch)
sha256sums=('3df1bbd72df82a905b6f6a6eaac3def17b0f586f578678f55d22ee7cc59dd7d0'
            'a50a3bbf35f0535f7e8e20af1893446a2e5711015484f9ae6d1ff91af3b23c4e'
            '30481dab7e21b66257e3238f5a8b708e1a5c4797d7da904f7ebaba4c2c2ef167')


prepare() {
  cd "$pkgbase-$pkgver"

  # Make compatible with Python 3 and Gstreamer 1.0
  patch -Np1 -i ../bugreport.patch
}

build() {
  cd "$pkgbase-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/totem \
    --localstatedir=/var \
    --disable-static \
    --enable-python \
    --enable-nautilus

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package_totem() {
  groups=(gnome)
  install=totem.install
  optdepends=('gst-plugins-ugly: Extra media codecs'
              'gst-libav: Extra media codecs'
              'lirc-utils: Infrared Remote Control plugin'
              'grilo-plugins: Browse sidebar (remote media)'
              'zeitgeist: Zeitgeist plugin')

  cd "$pkgbase-$pkgver"
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir/usr/lib/mozilla"
  rm "$pkgdir/usr/lib/totem/totem-plugin-viewer"
  rm "$pkgdir/usr/share/totem/org_gnome_totem_PluginViewer.xml"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/totem/COPYING"
}

package_totem-plugin() {
  groups=(gnome)
  pkgdesc="Totem plugin for web browsers"
  depends=("totem=$pkgver" dbus-glib)
  backup=(etc/totem/browser-plugins.ini)

  cd "$pkgbase-$pkgver"
  make -C browser-plugin \
    plugindir=/usr/lib/mozilla/plugins \
    xptdir=/usr/lib/mozilla/plugins \
    DESTDIR="$pkgdir" install

  install -Dm644 ../browser-plugins.ini "$pkgdir/etc/totem/browser-plugins.ini"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/totem-plugin/COPYING"
}
